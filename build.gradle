plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group = 'dev.array21'
version = '1.0.1'

findProperty('')

sourceCompatibility = '11'
targetCompatibility = '11'

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            name = "ossrh"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "ClassValidator"
                artifactId = "classvalidator"
                packaging = "jar"
                description = "Class Validation library"
                url = "https://github.com/TobiasDeBruijn/ClassValidator"

                scm {
                    connection = 'scm:git:git://github.com/TobiasDeBruijn/ClassValidator.git'
                    developerConnection = 'scm:git:ssh://github.com:TobiasDeBruijn/ClassValidator.git'
                    url = 'https://github.com/TobiasDeBruijn/ClassValidator/tree/master'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'tobiasdebruijn'
                        name = 'Tobias de Bruijn'
                        email = 't.debruijn@array21.dev'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}